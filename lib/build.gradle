apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'paulpv-android-material-range-bar-lib'
description = 'Material Range Bar Library for Android'
group = 'com.swooby'
version = '1.3.4'

def siteUrl = 'https://github.com/paulpv/material-range-bar'
def gitUrl = 'https://github.com/paulpv/material-range-bar.git'

dependencies {
    compile 'com.android.support:support-v4:23.4.0'
}

android {

    compileSdkVersion 23
    buildToolsVersion '23.0.3'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 23
        versionCode 1
        versionName '1.0'
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apikey')

    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = archivesBaseName
        userOrg = 'swooby'
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ['MIT']
    }
}

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'

                // Add your description here
                name description
                url siteUrl

                // Set your license
                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://raw.githubusercontent.com/SmartFoo/smartfoo/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id 'paulpv'
                        name 'Paul Peavyhouse'
                        email 'pv@swooby.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}